= Creating Network resources in AWS using Terraform
Diego Goytia <Diego.Goytia@ibm.com>
1.0 ,October 8, 2020


*Important note: This whole documentation is based on the training Deploying to AWS with Terraform and Ansible on ACloudGuru.*



== Amazon VPC

Amazon VPC is the networking layer for Amazon EC2.

* Concepts:
** A virtual network dedicated to your AWS account.
** *Subnet*: A range of IP addresses in your VPC.
** *Route table*: A set of rules, that are used to determine where traffic is directed.
** *Internet gateways*: It is a networking device used to communicate your VPC with the internet.
** *VPC Endpoint*: Enables you to privately connect your VPC to supported AWS Services, and VPC services powered by PrivateLink without requiring internet gateway, NAT device, VPN Connection or AWS Direct Connect. Instances in your VPC do not require public IP addresses to communicate resources via the service. Traffic between VPC and other services does not leave the Amazon network.



== Creating a Amazon VPC resource

It is required for creating hosted Network for you cloud.

[source, HCL]
----
# Create VPC in us-east-1
resource "aws_vpc" "vpc_master" {
  provider             = aws.region-master <1>
  cidr_block           = "10.0.0.0/16" <2>
  enable_dns_support   = true
  enable_dns_hostnames = true
  tags = {
    Name = "master-vpc-jenkins" <3>
  }
}
----

<1> the region-master parameter is set in the variables file, remember it defaults to "us-east-1".
<2> This is the CIDR block that the VPC is going to use. The CIDR is an allocated range of IP addresses
<3> It is good practice to tag all the resources you create on AWS.




== Creating an Internet Gateway Resource

Required to communicate your VPC with the internet

[source, HCL]
----
# Create IGW in us-east-1
resource "aws_internet_gateway" "igw" {
  provider = aws.region-master <1>
  vpc_id   = aws_vpc.vpc_master.id <2>
}
----

<1> The region-master defaults to us-east-1 as defined in variables.tf file.
<2> The vpc_id is the id associated with a previously deployed VPC. 


== Subnetting in the VPC.

*Getting the availability zone information*

An availability zone is is one or more discrete data center with reduntant power, networking and connectivity in an AWS Region. Basically, a Region consists of multiple availability zones. Therefore in order to create subsequent subnets, you'll have to find the first avilability zone that is in an available state.

_note: does this ensure any form of high availability? I suspect that yes._



[source, HCL]
----
# Get all available AZ's in VPC for master region
data "aws_availability_zones" "azs" {
  provider = aws.region-master
  state    = "available"
}

----

*Creating the subnet for the VPC.*

You can create as many subnets as you would like on your VPC and will depend entirely on your subnetting result and network solution.

For this example, I'll give you two different subnets attached to the same vpc in region us-east-1.

[source, HCL]
----
# Create a subnet # 1 in us-east-1
resource "aws_subnet" "subnet_1" {
  provider          = aws.region-master 
  availability_zone = element(data.aws_availability_zones.azs.names, 0) <1>
  vpc_id            = aws_vpc.vpc_master.id
  cidr_block        = "10.0.1.0/24"
}


# Create a subnet # 2 in us-east-1
resource "aws_subnet" "subnet_2" {
  provider          = aws.region-master
  availability_zone = element(data.aws_availability_zones.azs.names, 1) <2>
  vpc_id            = aws_vpc.vpc_master.id
  cidr_block        = "10.0.2.0/24"
}
----

Please note how <1> and <2> refer to different availability zones listed in the region us-east-1 (remember that region-master defaults to us-east-1). This ensures that each of this subnets is created in a different availabilty zones, and with two different subnets, one starting *10.0.1.0/24* and the second one starting *10.0.2.0/24*



For another example, let's see how you would create another subnet that doesn't require to be spread across different availability zones in another region (us-west-2)

[source, HCL]
----
# Create a subnet # 1 in us-west-2
resource "aws_subnet" "subnet_1_oregon" {
  provider   = aws.region-worker
  vpc_id     = aws_vpc.vpc_master_oregon.id
  cidr_block = "192.168.1.0/24" <1>
}
----

<1> In this example, you only have to give the name of the subnet, and this subnet will get created regardless of the availability zone in the region. 



== Communicating two VPCs

In order to communicate two VPC regions, you will have to deploy a peering connection request from one of the VPC's, and accept such peering request in the peer VPC.


*Creating the peering request*

[source, HCL]
----
# Create peering request
# Initiate Peering connection request from us-east-1
resource "aws_vpc_peering_connection" "useast1-uswest2" { <1>
  provider    = aws.region-master <2>
  peer_vpc_id = aws_vpc.vpc_master_oregon.id <3>
  vpc_id      = aws_vpc.vpc_master.id <4>
  peer_region = var.region-worker <5>
}
----

<1>In order to create the VPC peering request you'll have to understand that the peering request comes out from a specific VPC, and is accepted at a target VPC. In this case, we're initializing the request form us-east-1, and accepting it at our region us-west-2.
<2> The provider of the peering request will come from us-east-1
<3> The associated id for the secondary VPC created in another region.
<4> The associated id for the primary VPC.
<5> The region associated with the secondary VPC. In this case, remember that region-worker defaults to us-west-2 in our _variables.tf_ file.


*Accepting the VPC*

[source, HCL]
----
# Accept VPC peering request in us-west-2 from us-east-1
resource "aws_vpc_peering_connection_accepter" "accept_peering" {
  provider                  = aws.region-worker <1>
  vpc_peering_connection_id = aws_vpc_peering_connection.useast1-uswest2.id <2>
  auto_accept               = true <3>
}
----

<1> Accepting the request is originated from the target VPC, in this case region-worker defaults to us-west-2.
<2> The VPC peering request id created in the previous step.
<3> There are multiple methods (I'm guessing) for accepting the peering request. In this case, we're just auto-accepting the incoming request from vpc_master.


== Creating routing tables

In order to create a new routing table for our VPC, you'll have to create the desired rules for managing the traffic, and overwrite the default routing table of the VPC.

*Creating the routing table* 

[source, HCL]
----
# Create route table in us-east-1
resource "aws_route_table" "internet_route" {
  provider = aws.region-master
  vpc_id   = aws_vpc.vpc_master.id
  route {
    cidr_block = "0.0.0.0/0" <1>
    gateway_id = aws_internet_gateway.igw.id <2>
  }
  route {
    cidr_block                = "192.168.1.0/24" <3>
    vpc_peering_connection_id = aws_vpc_peering_connection.useast1-uswest2.id 
  }
  lifecycle {
    ignore_changes = all
  }
  tags = { <4>
    Name = "Master-Region-RT"
  }
}
----

<1> Is the address that represents the traffic coming from the internet, so we say that the vpc_master accepts traffic incoming from the internet.
<2> It is the associated ID with our internet gateway form VPC in us-east-1
<3> It is the Subnet from VPC in us-west-2 that we want to accept in our VPC in us-east-1, and it's associated id.
<4> It is good practice to always tag everythin you create in AWS. 


In order to make this changes effective, you will have to overwrite the default route table in your VPC using the following:

[source, HCL]
----
# Overwrite default route table of VPC(Master) with our route table entires
resource "aws_main_route_table_association" "set-master-default-rt-assoc" {
  provider       = aws.region-master
  vpc_id         = aws_vpc.vpc_master.id
  route_table_id = aws_route_table.internet_route.id <1>
}
----
<1> The id of the newly created internet route.
